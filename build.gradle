plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false
    id "com.palantir.git-version" version "0.12.0-rc2"
}

group "com.staffbase.scim2"
version gitVersion()

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if (!hasProperty("ossrhUsername")) {
    ext.ossrhUsername = ""
}
if (!hasProperty("ossrhPassword")) {
    ext.ossrhPassword = ""
}
if (!hasProperty("nexusUrl")) {
    ext.nexusUrl = ""
}
if (!hasProperty("nexusUsername")) {
    ext.nexusUsername = ""
}
if (!hasProperty("nexusPassword")) {
    ext.nexusPassword = ""
}

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "maven"
    apply plugin: "java-library"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.1.3.RELEASE"
        }
    }
}

subprojects {
    apply plugin: "signing"

    group = rootProject.group
    archivesBaseName = project.name
    version = rootProject.version

    dependencies {
        implementation group: "com.google.guava", name: "guava", version: "$guava_version"

        compileOnly group: "org.projectlombok", name: "lombok"
        annotationProcessor group: "org.projectlombok", name: "lombok"

        testImplementation "junit:junit"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://maven.pkg.github.com/${System.env.GITHUB_REPOSITORY}") {
                    authentication(userName:  "${System.env.GITHUB_ACTOR}", password:  "${System.env.GITHUB_TOKEN}")
                }

                snapshotRepository(url: nexusUrl) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name "SCIM 2.0 SDK for Spring"
                    packaging "jar"
                    description "SCIM 2.0 SDK for Spring"
                    url "https://github.com/Staffbase/scim2"

                    scm {
                        connection "https://github.com/Staffbase/scim2.git"
                        developerConnection "https://github.com/Staffbase/scim2.git"
                        url "https://github.com/Staffbase/scim2"
                    }

                    licenses {
                        license {
                            name "MIT"
                            url "https://github.com/Staffbase/scim2/blob/master/README.md"
                        }
                    }

                    developers {
                        [
                                developer {
                                    id "daniel-lynch"
                                    name "Daniel Lynch"
                                    email "dantlynch@gmail.com"
				},
                                developer {
                                    id "michael-stummvoll"
                                    name "Michael Stummvoll"
                                    email "michael.stummvoll@staffbase.com"
                                }
                        ]
                    }
                }
            }
        }
    }
}

uploadArchives {
    dependsOn = subprojects.uploadArchives
    enabled = false
}

wrapper {
    gradleVersion = '5.2.1'
}
